plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def libraryVersionCode = 3003000
def libraryVersionName = "3.4.5"

android {
    namespace 'com.dantsu.thermalprinter'
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode libraryVersionCode
        versionName libraryVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.zxing:core:3.4.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.6.1"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.DantSu'
                artifactId = 'ESCPOS-ThermalPrinter-Android'
                version = libraryVersionName

                pom {
                    name = 'Android library for ESC/POS Thermal Printer'
                    description = 'Useful library to help Android developers to print with (Bluetooth, TCP, USB) ESC/POS thermal printers.'
                }

                // Use the AAR file as the artifact for the library
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            }
        }

        repositories {
            maven {
                url = uri("$buildDir/repo")
            }
        }
    }

    // Ensure task dependencies are explicitly declared
    tasks.named("publishReleasePublicationToMavenLocal").configure {
        dependsOn("bundleReleaseAar")
    }
}
